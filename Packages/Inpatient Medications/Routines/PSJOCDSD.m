PSJOCDSD ; BIR/MV,RN - PROCESS DOSING ORDER CHECKS ;6 Jun 07  3:37 PM
 ;;5.0;INPATIENT MEDICATIONS;**181,252,281**;16 DEC 97;Build 113
 ;
DISPLAY ;Display dose checks
 NEW PSJPON,PSJDSPFG,PSJCNT0,DR
 D FULL^VALM1
 Q:'$$DSPSERR^PSJOC("Maximum Single Dose Check could not be performed")
 D EXCEPTN2
 K PSJDSPFG
 F PSJCNT0=0:0 S PSJCNT0=$O(^TMP($J,"PSJPRE1","OUT",PSJCNT0)) Q:'PSJCNT0  Q:$G(PSGORQF)  D
 . S PSJPON="" F  S PSJPON=$O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON)) Q:PSJPON=""  Q:$G(PSGORQF)  D
 .. Q:PSJPON=0
 .. D ERROR
 .. D EXCEPTN
 .. D WARNING
 K PSJDSPFG
 ;store dosing messages
 S (ZLERF,ZDSEQ,ZDLNN1)="",ZCNT=0
 F  S ZDSEQ=$O(^TMP($J,"PSJPRE","OUT","DOSE","ERROR",ZDSEQ)) Q:ZDSEQ=""  D
 .F  S ZCNT=$O(^TMP($J,"PSJPRE","OUT","DOSE","ERROR",ZDSEQ,ZCNT)) Q:'ZCNT  D
 ..S ^TMP("PSJDAOC",$J,"DOSE","ERROR",ZCNT)=^TMP($J,"PSJPRE","OUT","DOSE","ERROR",ZDSEQ,ZCNT,"MSG")_". "_^TMP($J,"PSJPRE","OUT","DOSE","ERROR",ZDSEQ,ZCNT,"TEXT")
 K ZLERF,ZDSEQ,ZDLNN1,ZCNT
 ;
 S (ZLERF,ZDSEQ,ZDLNN1)="",(ZDEF,ZCNT)=0
 F  S ZDEF=$O(^TMP($J,"PSJPRE1","OUT",ZDEF)) Q:'ZDEF  F  S ZDSEQ=$O(^TMP($J,"PSJPRE1","OUT",ZDEF,ZDSEQ)) Q:ZDSEQ=""  D
 .I $O(^TMP($J,"PSJPRE1","OUT",ZDEF,ZDSEQ,"EXCEPTIONS",0))  D
 ..S ZCNT=ZCNT+1
 ..S ^TMP("PSJDAOC",$J,"DOSE","EXEC",ZCNT)=^TMP($J,"PSJPRE1","OUT",ZDEF,ZDSEQ,"EXCEPTIONS",1)_". "_$G(^TMP($J,"PSJPRE1","OUT",ZDEF,ZDSEQ,"EXCEPTIONS",2))
 K ZLERF,ZDSEQ,ZDLNN1,ZCNT,ZDEF
 ;
 S (ZLERF,ZDSEQ,ZDLNN1)="",(ZDEF,ZCNT,ZTOT)=0
 F  S ZDEF=$O(^TMP($J,"PSJPRE1","OUT",ZDEF)) Q:'ZDEF  F  S ZDSEQ=$O(^TMP($J,"PSJPRE1","OUT",ZDEF,ZDSEQ)) Q:ZDSEQ=""  D
 .F  S ZLERF=$O(^TMP($J,"PSJPRE1","OUT",ZDEF,ZDSEQ,"MESSAGE",ZLERF)) Q:ZLERF=""  F  S ZCNT=$O(^TMP($J,"PSJPRE1","OUT",ZDEF,ZDSEQ,"MESSAGE",ZLERF,ZCNT)) Q:'ZCNT  D
 ..I $D(^TMP($J,"PSJPRE1","OUT",ZDEF,ZDSEQ,"MESSAGE",ZLERF,ZCNT))>9 D
 ...N TMP
 ...S TMP=""
 ...F  S TMP=$O(^TMP($J,"PSJPRE1","OUT",ZDEF,ZDSEQ,"MESSAGE",ZLERF,ZCNT,TMP)) Q:(TMP="")  D
 ....S ZTOT=ZTOT+1
 ....S ^TMP("PSJDAOC",$J,"DOSE","MSG",ZTOT)=^TMP($J,"PSJPRE1","OUT",ZDEF,ZDSEQ,"MESSAGE",ZLERF,ZCNT,TMP)
 ..E  D
 ...I $D(^TMP($J,"PSJPRE1","OUT",ZDEF,ZDSEQ,"MESSAGE",ZLERF,ZCNT)) D
 ....S ZTOT=ZTOT+1
 ....S ^TMP("PSJDAOC",$J,"DOSE","MSG",ZTOT)=^TMP($J,"PSJPRE1","OUT",ZDEF,ZDSEQ,"MESSAGE",ZLERF,ZCNT)
 K ZLERF,ZDSEQ,ZDLNN1,ZCNT,ZDEF,ZTOT,TMP
 Q
DOSEOFF(PSJMSG) ;
 ;Display message if dosing had turned off (once per patient session)
 Q:$D(PSJEXCPT("DOSE",0))
 S PSJEXCPT("DOSE",0)=""
 W !!,$G(PSJMSG),!
 D PAUSE^PSJLMUT1
 Q
WARNING ;Display warning messages
 NEW PSJSGLE,PSJRNGE,PSJMSG,PSJDD,PSJTYPE,PSJIOFF
 ;W @IOF
 S PSJMSG=""
 S (PSJSGLE,PSJRNGE)=0
 I '$O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",0)),'$O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"ERROR",0)) W !
 S PSJTYPE="" F  S PSJTYPE=$O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"MESSAGE",PSJTYPE)) Q:PSJTYPE=""  D
 . W:'$G(PSJIOFF) @IOF S PSJIOFF=1 W !
 . F PSJDD=0:0 S PSJDD=$O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"MESSAGE",PSJTYPE,PSJDD)) Q:'PSJDD  D
 .. Q:$G(PSGORQF)
 .. S PSJMSG=$G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"MESSAGE",PSJTYPE,PSJDD))
 .. I ($Y+6)>IOSL D PAUSE^PSJLMUT1 W @IOF
 .. D WRITE^PSJMISC(PSJMSG,3)
 .. S:PSJTYPE["1_SINGLE" PSJSGLE=1_U_PSJDD
 .. S:PSJTYPE["2_RANGE" PSJRNGE=1_U_PSJDD
 .. S:PSJTYPE["3_GENERAL" PSJDSPFG=1
 Q:$G(PSGORQF)
 D INTERV
 Q
INTERV ;Process intervention for dosing check
 NEW PSJDD
 S PSJDD=$S(+PSJSGLE:$P(PSJSGLE,U,2),1:$P(PSJRNGE,U,2))
 I 'PSJDD,'$D(PSJOCFG) Q
 K PSJDSPFG
 W !
 I +PSJSGLE,+PSJRNGE D RINTERV^PSJGMRA("MAX SINGLE DOSE & DAILY DOSE RANGE") Q
 I +PSJRNGE D RINTERV^PSJGMRA("DAILY DOSE RANGE") Q
 I +PSJSGLE D RINTERV^PSJGMRA("MAX SINGLE DOSE") Q
 Q
ERROR ; Process errors
 NEW PSJCNT,PSJNV,PSJLINEF
 ;PSJDSPFG - Display pause if there were errors
 ;Check for system error one more time.
 S PSJDSPFG=0
 ;I $O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"ERROR",0)) W !!
 F PSJCNT=0:0 S PSJCNT=$O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"ERROR",PSJCNT)) Q:'PSJCNT  D
 . I '$G(PSJLINEF) W !! S PSJLINEF=1
 . W !
 . S PSJNV=$G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"ERROR",PSJCNT,"MSG"))
 . I PSJNV]"" D WRITE^PSJMISC(PSJNV,1) S PSJDSPFG=1
 . S PSJNV=$G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"ERROR",PSJCNT,"TEXT"))
 . I PSJNV]"" D WRITE^PSJMISC(PSJNV,1) S PSJDSPFG=1
 D:$G(PSJDSPFG) PAUSE^PSJLMUT1
 Q
EXCEPTN ; Process exceptions
 NEW PSJCNT,PSJNV,PSJSPACE,PSJQFLG1,PSJLINEF
 ;PSJDSPFG - Display pause if there were errors
 ;Check for system error one more time.
 ;PSJOCFG - flag when order is Renew, Copy or New OE
 S PSJDSPFG=0,PSJSPACE=0
 ;I $O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",0)) W !!
 F PSJCNT=0:0 S PSJCNT=$O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",PSJCNT)) Q:'PSJCNT  D
 . I '$G(PSJLINEF) W !! S PSJLINEF=1
 . S PSJQFLG1=0
 . ;I $D(PSJOCFG),$G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",PSJCNT))["Dosing Checks could not be performed",($G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",PSJCNT+1))["Drug not matched to NDF") Q
 . I $D(PSJOCFG),($G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",PSJCNT+1))["Drug not matched to NDF") Q
 . I $D(PSJOCFG),$G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",PSJCNT))["Drug not matched to NDF" Q
 . I $D(PSJOCFG),$G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",PSJCNT))["Order Checks could not be done" Q
 . I $D(PSJOCFG),$G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",PSJCNT))["Maximum Single Dose Check could not be done" Q
 . S PSJDSPFG=1
 . S PSJNV=$G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",PSJCNT))
 . I PSJNV]"" D
 .. I ($Y+6)>IOSL D PAUSE^PSJLMUT1 W @IOF
 .. ;If the output has 13 leading spaces, strip it off so the display is indenting correctly
 .. I $E(PSJNV,1,13)="             " S PSJSPACE=13,PSJNV=$P(PSJNV,"             ",2)
 .. D WRITE^PSJMISC(PSJNV,PSJSPACE+3)
 D:$G(PSJDSPFG) PAUSE^PSJLMUT1
 Q
EXCEPTN2 ; Process exceptions on prospective drug
 NEW PSJPON,PSJN,PSJNV
 K PSJDSPFG
 S PSJPON="" F  S PSJPON=$O(^TMP($J,"PSJPRE","OUT","EXCEPTIONS",PSJPON)) Q:PSJPON=""  D
 . F PSJN=0:0 S PSJN=$O(^TMP($J,"PSJPRE","OUT","EXCEPTIONS",PSJPON,PSJN)) Q:'PSJN  D
 .. S PSJNV=$G(^TMP($J,"PSJPRE","OUT","EXCEPTIONS",PSJPON,PSJN))
 .. I $P(PSJPON,";",3)="PROFILE" Q
 .. I '$$ERRCHK^PSJOC("PROSPECTIVE",$P(PSJNV,U,3)_$P(PSJNV,U,10)) Q
 .. Q:'$D(PSJOCFG)
 .. W !
 .. D DSPDRGER^PSJOC(1)
 I $G(PSJDSPFG) D PAUSE^PSJLMUT1 W @IOF
 Q
