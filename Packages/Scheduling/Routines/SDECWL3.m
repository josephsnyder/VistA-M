SDECWL3 ;ALB/SAT - VISTA SCHEDULING RPCS ;JAN 15, 2016
 ;;5.3;Scheduling;**627**;Aug 13, 1993;Build 249
 ;
 Q
 ;
WLHIDE(SDECY,DFN,WLCL) ;GET wait list entries in which the associated clinic's 'HIDE FROM DISPLAY?' field is 'YES'
 ;WLHIDE(SDECY,DFN,WLCL)  external parameter tag in SDEC
 ; INPUT:
 ;   DFN   = (optional) Patient ID pointer to PATIENT file 2
 ;   WLCL  = (optional) Clinic ID pointer to SD WL CLINIC LOCATION
 ; RETURN:
 ;  DFN
 ;  ORIGDT   = Originating Date
 ;  TYPE     = Wait List Type
 ;  CLINIEN  = Clinic IEN pointer to HOSPITAL LOCATION file 44
 ;  WLCLNAME = WL SPECIFIC CLINIC
 ;  USERIEN  = Originating User
 ;  USERNAME = Originating User name
 ;  DATE1    = Date/Time Entered
 ;  DAPTDT   = Desired Date of appointment
 ;  STATUS   = Current Status
 ;               OPEN   CLOSED
 N CLINIEN,DAPTDT,DATE1,ORIGDT,STATUS,TYPE,USERIEN,USERNAME,WLCLIEN,WLCLNAME
 N SDI,SDCL,SDCL1,SDECI,SDDATA,INACTIVE,SDFIELDS,SDTMP,PTNAME
 N WLIEN
 S SDCL=""
 S SDECI=0
 S SDECY=$NA(^TMP("SDECWL3",$J,"WLHIDE"))
 K @SDECY
 S SDTMP="I00030DFN^T00030ORIGDT^T00030TYPE^T00030CLINIEN^T00030WLCLNAME^T00030USERIEN^"
 S SDTMP=SDTMP_"T00030USERNAME^T00030DATE1^T00030DAPTDT^T00030STATUS^T00030PATIENTNAME"_$C(30)
 S @SDECY@(SDECI)=SDTMP
 S DFN=$G(DFN)
 I DFN'="" I '$D(^DPT(DFN,0)) S @SDECY@(1)="-1^Invalid Patient ID." Q
 S WLCL=$G(WLCL)
 I +WLCL D
 .S SDI=0 F  S SDI=$O(^SDWL(409.32,"B",WLCL,SDI)) Q:SDI=""  D   ;Need to get the correct IEN
 ..S INACTIVE=$$GET1^DIQ(409.32,SDI_",",3,"I")
 ..I (INACTIVE'="")&($P(INACTIVE,".",1)<=$P($$NOW^XLFDT,".",1)) Q
 ..S (SDCL,SDCL1)=$$GET1^DIQ(409.32,+SDI_",",.01,"I")
 ;I +WLCL,SDCL="" S @SDECY@(1)="-1^Invalid Clinic Location ID." Q
 I +DFN D
 .I 'WLCL S (SDCL,SDCL1)=0
 .E  S SDCL=WLCL-1
 .F  S SDCL=$O(^SDWL(409.3,"AD",DFN,SDCL)) Q:SDCL'>0  Q:(WLCL>0)&(WLCL'=SDCL)  D
 ..Q:$P($G(^SC(SDCL,0)),U,26)'=1
 ..S WLIEN=0 F  S WLIEN=$O(^SDWL(409.3,"AD",DFN,SDCL,WLIEN)) Q:WLIEN'>0  D GET1
 G:DFN'="" XIT
 S SDCL1=+SDCL
 S SDCL=$S(+SDCL:SDCL-1,1:0) F  S SDCL=$O(^SC("AF",1,SDCL)) Q:SDCL'>0  Q:(SDCL1>0)&(SDCL1'=SDCL)  D
 .S WLIEN=0 F  S WLIEN=$O(^SDWL(409.3,"AE",SDCL,WLIEN)) Q:WLIEN'>0  D GET1
XIT ;
 S @SDECY@(SDECI)=@SDECY@(SDECI)_$C(31)
 Q
 ;
GET1 ;
 K SDDATA
 Q:$P($G(^SDWL(409.3,WLIEN,0)),U,17)="C"
 S SDFIELDS=".01;1;4;8;8.5;9;9.5;22;23"
 D GETS^DIQ(409.3,WLIEN,SDFIELDS,"IE","SDDATA")
 S DFN=SDDATA(409.3,WLIEN_",",.01,"I")    ;DFN
 S PTNAME=$$GET1^DIQ(2,DFN,.01)           ;NAME OF PT
 S ORIGDT=SDDATA(409.3,WLIEN_",",1,"E")   ;ORIGINATING DATE
 S TYPE=SDDATA(409.3,WLIEN_",",4,"E")     ;WAIT LIST TYPE
 S CLINIEN=SDDATA(409.3,WLIEN_",",8.5,"I")  ;CLINIC IEN
 I CLINIEN="" D
 .S WLCLIEN=SDDATA(409.3,WLIEN_",",8,"I")
 .S CLINIEN=$$GET1^DIQ(409.32,WLCLIEN_",",.01,"I")
 Q:CLINIEN=""
 S WLCLNAME=$$GET1^DIQ(44,CLINIEN_",",.01)  ;Clinic name
 S USERIEN=SDDATA(409.3,WLIEN_",",9,"I")  ;ORIGINATING USER
 S USERNAME=SDDATA(409.3,WLIEN_",",9,"E") ;ORIGINATING USER name
 S DATE1=SDDATA(409.3,WLIEN_",",9.5,"E")  ;DATE/TIME ENTERED
 S DAPTDT=SDDATA(409.3,WLIEN_",",22,"E")  ;Desired Date of Appointment
 S STATUS=SDDATA(409.3,WLIEN_",",23,"E")  ;CURRENT STATUS
 S SDTMP=DFN_U_ORIGDT_U_TYPE_U_CLINIEN_U_WLCLNAME_U_USERIEN_U_USERNAME
 S SDTMP=SDTMP_U_DATE1_U_DAPTDT_U_STATUS_U_PTNAME
 S SDECI=SDECI+1 S @SDECY@(SDECI)=SDTMP_$C(30)
 Q
