SDEC17 ;ALB/SAT - VISTA SCHEDULING RPCS ;JAN 15, 2016
 ;;5.3;Scheduling;**627**;Aug 13, 1993;Build 249
 ;
 Q
 ;
SCHUSR(SDECY) ;Return recordset of all users in NEW PERSON having SDECZMENU and/or SDECZMGR key
 ;SCHUSR(SDECY)  external parameter tag is in SDEC
 ;
 N SDECDUZ,SDECKEY,SDECI,SDECNAM,SDECKEYN
 S SDECY="^TMP(""SDEC"","_$J_")"
 K @SDECY
 K ^TMP($J,"SDEC17")
 S SDECI=0
 S @SDECY@(0)="I00020USERID^T00030USERNAME"_$C(30)
 ;$O Through ^VA(200,"AB",
 F SDECKEYN="SDECZMENU","SDECZMGR" S SDECKEY=+$O(^DIC(19.1,"B",SDECKEYN,0)) D
 . Q:'+SDECKEY  S SDECDUZ=0 F  S SDECDUZ=$O(^VA(200,"AB",SDECKEY,SDECDUZ)) Q:'+SDECDUZ  D
 . . Q:SDECDUZ<1
 . . Q:'$D(^VA(200,SDECDUZ,0))
 . . Q:$D(^TMP($J,"SDEC17",SDECDUZ))
 . . S SDECNAM=$P(^VA(200,SDECDUZ,0),U)
 . . S SDECI=SDECI+1
 . . S ^TMP($J,"SDEC17",SDECDUZ)=""
 . . S @SDECY@(SDECI)=SDECDUZ_"^"_SDECNAM_$C(30)
 . . Q
 . Q
 ;
 S @SDECY@(SDECI)=@SDECY@(SDECI)_$C(31)
 Q
