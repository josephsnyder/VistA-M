DVBABEBD ;ALB - CAPRI EXAM BY DATE REPORT;09/13/04 ; 2/10/11 12:12pm
 ;;2.7;AMIE;**57,108,185**;Apr 10, 1995;Build 18
 Q
 ;
EXAMBYDT(MSG,BEGDT,ENDDT,DVBADLMTR) ; CALLED BY REMOTE PROCEDURE DVBAB EXAMS BY DATE
 N DVBABCNT,DVBABIEN,DVBABPAT,DVBABT,DVBAB0,DVBABT1,DVBABT2,DVBABT3,DVBABT4,DVBABT5,DVBABT6,DFN
 S DVBADLMTR=$S($G(DVBADLMTR)=1:",",1:0)
 K ^TMP("DVBABEBD",DUZ) D SETSTAT
 I $G(DVBADLMTR)="," D
 . S ^TMP("DVBABEBD",DUZ,1)="SSN"_DVBADLMTR_"PATIENT NAME"_DVBADLMTR_"REQUEST DATE"_DVBADLMTR_"DATE RELEASED"_DVBADLMTR_"DATE PRINTED BY RO"_DVBADLMTR_"REQUEST STATUS"_$C(13)
 S DVBABCNT=$S(DVBADLMTR'=",":1,1:2),DVBABIEN=0,MSG=$NA(^TMP("DVBABEBD",DUZ))
 F  S DVBABIEN=$O(^DVB(396.3,DVBABIEN)) Q:+DVBABIEN=0  D 
 . ;REQUEST DATE
 . S DVBAB0=$G(^DVB(396.3,DVBABIEN,0)) Q:DVBAB0=""
 . S DVBABPAT=$G(^DPT($P(DVBAB0,"^"),0)) Q:DVBABPAT=""
 . S DVBABT2=$P($P(DVBAB0,"^",2),".")
 . I (DVBABT2>(BEGDT-1))&(DVBABT2<(ENDDT+1)) D
 .. S DVBABT1=$P(DVBABPAT,"^",1) ;PATIENT NAME
 .. S DFN=$P(DVBAB0,"^") D DEM^VADPT I $G(VADM(1))'="" S DVBABT6=$S($G(DVBADLMTR)=",":$P($G(VADM(2)),U,2),1:$P($G(VADM(2)),U,1))  ;SSN
 .. S DVBABT3=$P($P(DVBAB0,"^",14),".") ;DATE RELEASED
 .. S DVBABT4=$P($P(DVBAB0,"^",16),".") ;DATE PRINTED BY RO
 .. S DVBABT5=$P(DVBAB0,"^",18) ;REQUEST STATUS
 .. S:$D(DVBABT(DVBABT5)) DVBABT5=DVBABT(DVBABT5)
 .. I $G(DVBADLMTR)'="," S ^TMP("DVBABEBD",DUZ,DVBABCNT)=DVBABT1_U_$$FMTE^XLFDT(DVBABT2,"5DZ")_U_$$FMTE^XLFDT(DVBABT3,"5DZ")_U_$$FMTE^XLFDT(DVBABT4,"5DZ")_U_DVBABT5_U_DVBABT6_$C(13)
 .. I $G(DVBADLMTR)="," S ^TMP("DVBABEBD",DUZ,DVBABCNT)=DVBABT6_DVBADLMTR_""""_DVBABT1_""""_DVBADLMTR_$$FMTE^XLFDT(DVBABT2,"5DZ")_DVBADLMTR_$$FMTE^XLFDT(DVBABT3,"5DZ")_DVBADLMTR_$$FMTE^XLFDT(DVBABT4,"5DZ")_DVBADLMTR_""""_DVBABT5_""""_$C(13)
 .. S DVBABCNT=DVBABCNT+1
 Q
 ;
SETSTAT S DVBABT("N")="NEW"
 S DVBABT("P")="PENDING, REPORTED"
 S DVBABT("S")="PENDING, SCHEDULED"
 S DVBABT("R")="RELEASED TO RO, NOT PRINTED"
 S DVBABT("C")="COMPLETED, PRINTED BY RO"
 S DVBABT("X")="CANCELLED BY MAS"
 S DVBABT("RX")="CANCELLED BY RO"
 S DVBABT("T")="TRANSCRIBED"
 S DVBABT("NT")="NEW, TRANSFERRED IN"
 S DVBABT("CT")="COMPLETE, TRANSFERRED OUT"
 Q
